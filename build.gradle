buildscript {
    apply from: 'versions.gradle'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath plugin.agp
        classpath plugin.kotlin
    }
}

plugins {
    id "com.github.dcendents.android-maven" version "2.1"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "io.gitlab.arturbosch.detekt" version "1.10.0-RC1"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
        }
    }
}

detekt {
    toolVersion = versions.detekt
    config = files("$project.rootDir/config/detekt/detekt.yml")
    input  = files(
            "$project.rootDir/app/src/main/java",
            "$project.rootDir/lottie-dialog/src/main/java"
    )
    failFast = false
    parallel = true
    reports {
        xml.enabled  = true
        txt.enabled  = true
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    jvmTarget = "1.8"
}
